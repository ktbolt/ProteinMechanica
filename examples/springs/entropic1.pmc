#============================================================#
# rigid body simulation of a helix with entropic spring.     #
#                                                            #
# define rigid bodies for peptide plane and sidechains.      #
#                                                            #
# use multibody command.                                     #
#============================================================#
system echo=off
#system verbose=false

# set units
#----------
units scale name=mass uname=reducedMass value=10000.0

# read molecule
#--------------
database open name=db1 format=pdb file=../data/iq123.pdb
molecule read name=m1 database=db1

# create helix domain
#--------------------
domain create helix=C[766-813]
domain helix backbone color=[ 0.6 0.6 0.6 ]  width=2 show=true

# create kinematic chain 
#-----------------------
multibody  create  name=kc  domain=helix  type=kinematic_chain

# add ground body and joint
#--------------------------
body create  name=ground  type=ground

joint create  name=groundJnt  type=weld  position=kcSc766:C[766]:CA
joint groundJnt color=[ 1 1 1 ] msize=0.01 shading=color show=true
joint groundJnt bodies={ ground kcSc766Body }

# create a rigid simulation  
#---------------------------
simulation create name=rsim  type=rigid
simulation rsim add bodies=all
simulation rsim add joints=all

#simulation rsim print  joints=true
#simulation rsim print bodies=true 

# set solver
#-----------
simulation rsim solver=ode
simulation rsim solver finite_rotation=on

# add forces
#-----------
force create  name=force1             \
              type=explicit           \
              point=[ 0 0 0 ]         \
              global_frame=false      \
              direction=[ 10 10 0 ]   \
              scale=1.0

simulation rsim add force=force1       \
                    body=kcSc812Body   \
                    time={ 0 0.5 }


# add entropic spring potential
#------------------------------
domain kcSc766 define region=kcSc766Rgn residues=C[766] atom_names={ CA }
domain kcSc812 define region=kcSc812Rgn residues=C[812] atom_names={ CA }

body kcSc766Body add potential_geometry=spring1  \
   region=kcSc766Rgn geometry=points             \
   color=[1 0.8 0] show=true

body kcSc812Body add potential_geometry=spring2  \
   region=kcSc812Rgn geometry=points  show=true

simulation rsim add interaction=hspring1 \
   type=entropic-spring                  \
   function=wlc                          \
   persistence_length=0.4                \
   contour_length=4.0                    \
   list={spring1 spring2}                \
   show=true time={ 0.0 1000.0 }


# initialize simulation
#----------------------
simulation rsim initialize
simulation rsim time step=0.005
simulation rsim momentum=on
#simulation  state  save_frequency=10

#simulation rsim  add trace  name=trace1  color=[ 1 0 1 ]  \
           global_frame=true    \
           point=kcSc76:C[836]  \
           body=kcScBody76   show=true

# print body masses 
#------------------
#body  d1b  print  properties=mass
#body  d2b  print  properties=mass

graphics  center  point=[ 0.426082 0.721974 0.604492 ]
